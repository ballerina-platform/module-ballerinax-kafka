/*
 * Copyright (c) 2021, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * WSO2 Inc. licenses this file to you under the Apache License,
 * Version 2.0 (the "License"); you may not use this file except
 * in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'java'

description = 'Ballerina - Kafka Examples'

def ballerinaDist = "${project.rootDir}/target/ballerina-runtime"
def examples = [
    "twitter-filter/elasticsearch-consumer",
    "twitter-filter/mock-twitter-server",
    "twitter-filter/twitter-producer",
    "order-manager/order-service",
    "order-manager/order-processor",
    "order-manager/notification-service",
    "secured-word-count-calculator/word-count-calculator",
    "stock-trading-analyzer/trade-api",
    "stock-trading-analyzer/trade-analyzer",
]
def packageName = "kafka"

dependencies {
    implementation group: 'org.ballerinalang', name: 'ballerina-lang', version: "${ballerinaLangVersion}"
    implementation group: 'org.ballerinalang', name: 'ballerina-tools-api', version: "${ballerinaLangVersion}"
    implementation group: 'org.ballerinalang', name: 'ballerina-parser', version: "${ballerinaLangVersion}"
    implementation group: 'org.testng', name: 'testng', version: "${testngVersion}"
}

clean {
    examples.forEach { example ->
        delete "${projectDir}/${example}/target"
    }
}

task testExamples {
    doLast {
        examples.each { example ->
            try {
                exec {
                    workingDir project.projectDir
                    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                        commandLine 'cmd', '/c', "${ballerinaDist}/bin/bal.bat test --offline ${example} && exit %%ERRORLEVEL%%"
                    } else {
                        commandLine 'sh', '-c', "${ballerinaDist}/bin/bal test --offline ${example}"
                    }
                }
            } catch (Exception e) {
                println("Example '${example}' Build failed: " + e.message)
                throw e
            }
        }
    }
}

task buildExamples {
    gradle.taskGraph.whenReady { graph ->
        if (graph.hasTask(":${packageName}-examples:test")) {
            buildExamples.enabled = false
        } else {
            testExamples.enabled = false
        }
    }
    doLast {
        examples.each { example ->
            try {
                exec {
                    workingDir project.projectDir
                    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                        commandLine 'cmd', '/c', "${ballerinaDist}/bin/bal.bat build ${example} && exit %%ERRORLEVEL%%"
                    } else {
                        commandLine 'sh', '-c', "${ballerinaDist}/bin/bal build ${example}"
                    }
                }
            } catch (Exception e) {
                println("Example '${example}' Build failed: " + e.message)
                throw e
            }
        }
    }
}

task startKafkaServer() {
    doLast {
        if (!Os.isFamily(Os.FAMILY_WINDOWS)) {
            def stdOut = new ByteArrayOutputStream()
            exec {
                commandLine 'sh', '-c', "docker ps --filter name=kafka-test"
                standardOutput = stdOut
            }
            if (!stdOut.toString().contains("kafka-test")) {
                println "Starting Kafka server and waiting for it to be healthy..."
                exec {
                    commandLine 'sh', '-c', "docker compose -f ../ballerina/tests/resources/docker-compose.yaml up --wait"
                    standardOutput = stdOut
                }
            }
            println "Kafka server is ready."
        }
    }
}

task stopKafkaServer() {
    doLast {
        if (!Os.isFamily(Os.FAMILY_WINDOWS)) {
            def stdOut = new ByteArrayOutputStream()
            exec {
                commandLine 'sh', '-c', "docker ps --filter name=kafka-test"
                standardOutput = stdOut
            }
            if (stdOut.toString().contains("kafka-test")) {
                println "Stopping Kafka server."
                exec {
                    commandLine 'sh', '-c', "docker compose -f ../ballerina/tests/resources/docker-compose.yaml down"
                    standardOutput = stdOut
                }
                println stdOut.toString()
            } else {
                println "Kafka server is not started."
            }
        }
    }
}

buildExamples.dependsOn ":${packageName}-ballerina:build"
testExamples.dependsOn ":${packageName}-ballerina:build"
testExamples.dependsOn startKafkaServer
testExamples.finalizedBy stopKafkaServer

build.dependsOn buildExamples
test.dependsOn testExamples
