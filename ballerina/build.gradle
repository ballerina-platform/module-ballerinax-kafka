/*
 * Copyright (c) 2020, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

import org.apache.tools.ant.taskdefs.condition.Os

buildscript {
    repositories {
        maven {
            url = 'https://maven.pkg.github.com/ballerina-platform/plugin-gradle'
            credentials {
                username System.getenv("packageUser")
                password System.getenv("packagePAT")
            }
        }
    }
    dependencies {
        classpath "io.ballerina:plugin-gradle:${project.ballerinaGradlePluginVersion}"
    }
}

description = 'Ballerina - Kafka Ballerina'

def packageName = "kafka"
def packageOrg = "ballerinax"
def tomlVersion = stripBallerinaExtensionVersion("${project.version}")
def ballerinaTomlFilePlaceHolder = new File("${project.rootDir}/build-config/resources/Ballerina.toml")
def compilerPluginTomlFilePlaceHolder = new File("${project.rootDir}/build-config/resources/CompilerPlugin.toml")
def ballerinaConfigFile = new File("$project.projectDir/Ballerina.toml")
def ballerinaCompilerPluginFile = new File("$project.projectDir/CompilerPlugin.toml")
def originalConfig = ballerinaConfigFile.text
def originalCompilerPlugin = ballerinaCompilerPluginFile.text

def stripBallerinaExtensionVersion(String extVersion) {
    if (extVersion.matches(project.ext.timestampedVersionRegex)) {
        def splitVersion = extVersion.split('-')
        if (splitVersion.length > 3) {
            def strippedValues = splitVersion[0..-4]
            return strippedValues.join('-')
        } else {
            return extVersion
        }
    } else {
        return extVersion.replace("${project.ext.snapshotVersion}", "")
    }
}

apply plugin: 'io.ballerina.plugin'

ballerina {
    module = packageName
    langVersion = ballerinaLangVersion
    packageOrganization = packageOrg
}

configurations {
    externalJars
}

dependencies {
    externalJars(group: 'org.apache.kafka', name: 'kafka-clients', version: "${kafkaVersion}") {
        transitive = false
    }
    externalJars(group: 'org.apache.kafka', name: 'kafka_2.12', version: "${kafkaVersion}") {
        transitive = false
    }
    externalJars(group: 'org.apache.avro', name: 'avro', version: "${avroVersion}") {
        transitive = false
    }
}

task updateTomlFiles {
    doLast {
        def newConfig = ballerinaTomlFilePlaceHolder.text.replace("@project.version@", project.version)
        newConfig = newConfig.replace("@toml.version@", tomlVersion)
        newConfig = newConfig.replace("@kafka.version@", kafkaVersion)
        ballerinaConfigFile.text = newConfig

        def newPluginConfig = compilerPluginTomlFilePlaceHolder.text.replace("@project.version@", project.version)
        ballerinaCompilerPluginFile.text = newPluginConfig
    }
}

task commitTomlFiles {
    doLast {
        project.exec {
            ignoreExitValue true
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', "git commit Ballerina.toml CompilerPlugin.toml -m '[Automated] Commit the native jar versions'"
            } else {
                commandLine 'sh', '-c', "git commit Ballerina.toml CompilerPlugin.toml -m '[Automated] Commit the native jar versions'"
            }
        }
    }
}


task startKafkaServer() {
    doLast {
        if (!Os.isFamily(Os.FAMILY_WINDOWS)) {
            def stdOut = new ByteArrayOutputStream()
            exec {
                commandLine 'sh', '-c', "docker ps --filter name=kafka-test"
                standardOutput = stdOut
            }
            if (!stdOut.toString().contains("kafka-test")) {
                println "Starting Kafka server."
                exec {
                    commandLine 'sh', '-c', "docker-compose -f tests/docker-compose.yaml up -d"
                    standardOutput = stdOut
                }
                println stdOut.toString()
                sleep(5 * 1000)
            } else {
                println "Kafka server is already running."
            }
        }
    }
}

task stopKafkaServer() {
    doLast {
        if (!Os.isFamily(Os.FAMILY_WINDOWS)) {
            def stdOut = new ByteArrayOutputStream()
            exec {
                commandLine 'sh', '-c', "docker ps --filter name=kafka-test"
                standardOutput = stdOut
            }
            if (stdOut.toString().contains("kafka-test")) {
                println "Stopping Kafka server."
                exec {
                    commandLine 'sh', '-c', "docker-compose -f tests/docker-compose.yaml rm -svf"
                    standardOutput = stdOut
                }
                println stdOut.toString()
                sleep(5 * 1000)
            } else {
                println "Kafka server is not started."
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact source: createArtifactZip, extension: 'zip'
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ballerina-platform/module-${packageOrg}-${packageName}")
            credentials {
                username = System.getenv("publishUser")
                password = System.getenv("publishPAT")
            }
        }
    }
}

updateTomlFiles.dependsOn copyStdlibs

test.dependsOn startKafkaServer

build.dependsOn "generatePomFileForMavenPublication"
build.dependsOn ":${packageName}-native:build"
build.dependsOn ":${packageName}-compiler-plugin:build"
test.dependsOn ":${packageName}-native:build"
test.dependsOn ":${packageName}-compiler-plugin:build"
build.finalizedBy stopKafkaServer

publishToMavenLocal.dependsOn build
publish.dependsOn build
